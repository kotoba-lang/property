(ns kotoba.property.nyc-runtime-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.property.nyc-runtime :as nyc]))

(def observed-at "2026-08-01T10:00:00Z")

(deftest normalizes-a-row-into-the-ownership-contract
  (let [c (nyc/normalize-record observed-at {:bbl "1019090009.0" :agency "CULT"})]
    (is (= "US-NY-NYC:BBL:1019090009.0" (:ownership/parcel c)))
    (is (= "City of New York / CULT" (:ownership/holder c)))
    (is (= :public-body (:ownership/holder-kind c)))
    (is (= :public (:ownership/disclosure c)))
    (is (= "NYC Open Data Terms of Use" (:ownership/licence c)))))

(deftest two-agencies-on-one-parcel-are-two-claims
  ;; Regression: keying the claim id on the BBL alone collapsed every
  ;; multi-agency parcel to a single entity, dropping 391 of 2,088 claims.
  (let [a (nyc/normalize-record observed-at {:bbl "1000220020.0" :agency "DCA"})
        b (nyc/normalize-record observed-at {:bbl "1000220020.0" :agency "ELECT"})]
    (is (not= (:ownership/id a) (:ownership/id b)))
    (is (= (:ownership/parcel a) (:ownership/parcel b)))))

(deftest rows-missing-a-key-field-are-dropped
  (testing "no parcel or no holder means no claim, not a blank one"
    (is (nil? (nyc/normalize-record observed-at {:agency "CULT"})))
    (is (nil? (nyc/normalize-record observed-at {:bbl "1019090009.0"})))))
