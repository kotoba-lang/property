(ns kotoba.property.bulk-csv-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.property.bulk-csv :as csv]))

(deftest quotes-and-embedded-newlines
  (testing "引用の中の改行で行が割れない"
    (is (= [["a" "b\nc"] ["d" "e"]]
           (csv/parse "\"a\",\"b\nc\"\n\"d\",\"e\"\n"))))
  (testing "引用の中のカンマで割らない"
    (is (= [["a" "b,c"]] (csv/parse "\"a\",\"b,c\"\n"))))
  (testing "エスケープされた引用符"
    (is (= [["a\"b"]] (csv/parse "\"a\"\"b\"\n"))))
  (testing "BOM を落とす"
    (is (= [["法人番号"]] (csv/parse "﻿\"法人番号\"\n"))))
  (testing "引用が閉じないまま終わった断片を捨てない"
    (is (= 2 (count (csv/parse "\"a\",\"b\"\n\"c\",\"unterminated\n"))))))

(deftest getter-distinguishes-missing-column-from-empty-cell
  (let [rows (csv/parse "\"法人番号\",\"名称\"\n\"1\",\"\"\n")
        g (csv/getter (first rows))
        row (second rows)]
    (testing "在る列の空セルは空文字"
      (is (= "" (g row "名称"))))
    (testing "無い列は nil（空文字ではない）"
      (is (nil? (g row "証明日"))))
    (is (= "1" (g row "法人番号")))))

(deftest unclosed-quote-counts-pairs
  (is (false? (csv/unclosed-quote? "\"a\",\"b\"")))
  (is (true? (csv/unclosed-quote? "\"a\",\"b")))
  (is (false? (csv/unclosed-quote? "no quotes here"))))
