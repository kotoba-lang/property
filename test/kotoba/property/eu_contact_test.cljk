(ns kotoba.property.eu-contact-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.property.contact-point :as cp]
            [kotoba.property.eu-contact :as eu]
            [kotoba.property.eu-cordis :as cordis]))

(deftest email-classification-is-stricter-than-jp
  (testing "role 語彙そのものは :role"
    (doseq [e ["info@acme.de" "contact@acme.fr" "sales@acme.it" "hello@acme.se"]]
      (is (= :role (eu/classify-email e)) e)))
  (testing "role 語 + 国/言語コードは :role"
    (doseq [e ["info-de@acme.com" "contact.eu@acme.com" "sales_uk@acme.com"
               "info-global@acme.com"]]
      (is (= :role (eu/classify-email e)) e)))
  (testing "これが日本版との差 — 前置きが一致しても、続きが人名なら :personal"
    (is (= :role (cp/classify-email "sales.john@acme.com"))
        "日本版は :role に落とす")
    (is (= :personal (eu/classify-email "sales.john@acme.com"))
        "EU 版は落とさない"))
  (testing "TED が返してくるのはこの形。3 件とも落ちる"
    (doseq [e ["ragnar.johansson@svevia.se" "linus.springare@arcticinfra.se"
               "stefan.kostenniemi@bdx.se"]]
      (is (= :personal (eu/classify-email e)) e)))
  (testing "迷ったら :personal に倒す"
    (is (= :personal (eu/classify-email "j.dupont@acme.fr")))
    (is (= :personal (eu/classify-email "mueller@acme.de")))
    (is (= :personal (eu/classify-email "info.mueller@acme.de")))))

(deftest personal-emails-never-reach-output
  (let [rec (eu/->record {:organisation-id "1" :name "ACME"}
                         {:status :ok
                          :contact-url "https://acme.example/contact"
                          :emails [{:email "info@acme.example" :kind (eu/classify-email "info@acme.example")}
                                   {:email "anna.schmidt@acme.example"
                                    :kind (eu/classify-email "anna.schmidt@acme.example")}]
                          :observed-at "2026-08-25T00:00:00Z"})]
    (is (= ["info@acme.example"] (:contact/emails rec)))
    (is (= 1 (:contact/personal-emails-excluded rec))
        "落とした個人アドレスは件数だけ残す")))

(deftest eu-portal-form-is-recorded-but-not-a-sales-channel
  (let [rec (eu/->record {:organisation-id "1" :name "ACME"
                          :contact-form "https://ec.europa.eu/info/funding-tenders/opportunities/portal/screen/contact-form/project/1/2"}
                         {:status :no-contact-point :observed-at "2026-08-25T00:00:00Z"})]
    (testing "事実は消さない"
      (is (some? (:contact/eu-portal-form rec)))
      (is (= "eu-portal-form" (eu/channel rec))))
    (testing "使わない"
      (is (false? (eu/contactable? rec))))
    (testing "会社自身の窓口とキーが混ざらない"
      (is (nil? (:contact/form-url rec))))))

(deftest channel-values-are-closed
  (let [mk (fn [m] (eu/->record m {:status :ok :observed-at "t"}))]
    (is (= "web" (eu/channel (eu/->record {:name "A"} {:status :ok :observed-at "t"
                                                       :contact-url "https://a/contact"}))))
    (is (= "post" (eu/channel (mk {:name "A" :street "X 1" :city "Wien"}))))
    (is (= "none" (eu/channel (mk {:name "A"}))))
    (doseq [r [(mk {:name "A"}) (mk {:name "A" :street "X 1" :city "Wien"})
               (mk {:name "A" :contact-form "https://ec.europa.eu/x"})]]
      (is (contains? eu/channels (eu/channel r))))))

(deftest contactable-is-channel-based-not-status-based
  (let [ok (eu/->record {:name "A" :street "X 1" :city "Wien"} {:status :ok :observed-at "t"})]
    (is (true? (eu/contactable? ok)))
    (is (false? (eu/contactable? (assoc ok :contact/solicitation-forbidden? true)))))
  (testing "URL の無い会社は郵送で連絡できる — status で落とすと母集団の 9 割が消える"
    (let [no-site (eu/->record {:name "A" :street "X 1" :city "Wien"}
                               {:status :no-website :observed-at "t"})]
      (is (= "post" (eu/channel no-site)))
      (is (true? (eu/contactable? no-site)))))
  (testing "住所も取れない行は落ちる"
    (is (false? (eu/contactable? (eu/->record {:name "A"} {:status :no-website :observed-at "t"}))))))

(deftest coverage-splits-web-from-post
  (testing "実際に読んで営業お断りを確かめた行と、住所しか見ていない行を同じ顔で数えない"
    (let [recs [(eu/->record {:name "A" :street "X" :city "Wien"}
                             {:status :ok :observed-at "t" :contact-url "https://a/kontakt"})
                (eu/->record {:name "B" :street "Y" :city "Roma"}
                             {:status :no-website :observed-at "t"})
                (eu/->record {:name "C" :street "Z" :city "Paris"}
                             {:status :fetch-failed :observed-at "t"})]
          c (eu/coverage recs)]
      (is (= 3 (:coverage/contactable c)))
      (is (= 1 (:coverage/contactable-web c)))
      (is (= 2 (:coverage/contactable-post c)))
      (is (= 2 (:coverage/post-solicitation-unobserved c))
          "郵送 2 件とも、相手のページを読めていない"))))

(deftest status-domain-is-closed
  (is (thrown? #?(:clj Throwable :cljs js/Error)
               (eu/->record {:name "A"} {:status :whatever}))))

(deftest solicitation-refusal-in-eu-languages
  (testing "日本語の既定は EU で 1 語も当たらない — これが差し替えの理由"
    (is (false? (cp/solicitation-forbidden? "<p>Keine Werbung, bitte.</p>")))
    (is (true? (cp/solicitation-forbidden? "<p>Keine Werbung, bitte.</p>"
                                           eu/solicitation-forbidden-re))))
  (doseq [[lang html] {:de "<p>Keine Kaltakquise.</p>"
                       :de2 "<p>Werbeanrufe unerwünscht.</p>"
                       :fr "<p>Pas de démarchage commercial.</p>"
                       :en "<p>We do not accept unsolicited sales enquiries.</p>"
                       :en2 "<p>No solicitation, please.</p>"
                       :es "<p>No aceptamos publicidad.</p>"
                       :nl "<p>Geen ongevraagde reclame.</p>"
                       :sv "<p>Ingen telefonförsäljning tack.</p>"}]
    (is (true? (cp/solicitation-forbidden? html eu/solicitation-forbidden-re)) (str lang)))
  (testing "普通の連絡ページを営業お断りと読まない"
    (is (false? (cp/solicitation-forbidden?
                 "<p>Kontaktieren Sie uns gerne per E-Mail.</p>" eu/solicitation-forbidden-re)))))

(deftest multilingual-link-discovery
  (let [html (str "<a href=\"/impressum\">Impressum</a>"
                  "<a href=\"/kontakt\">Kontakt</a>"
                  "<a href=\"/x\">Contactez-nous</a>"
                  "<a href=\"/produkte\">Produkte</a>")
        found (cp/discover-contact-links html "https://acme.de" eu/vocabulary)]
    (is (= #{"https://acme.de/impressum" "https://acme.de/kontakt" "https://acme.de/x"}
           (set found)))
    (testing "日本語の既定では Kontakt も Impressum も拾えない"
      (is (empty? (remove #{"https://acme.de/x"} (cp/discover-contact-links html "https://acme.de")))))))

(deftest ranking-prefers-real-desks-over-about-pages
  (let [ranked (cp/rank-contact-candidates
                ["https://acme.de/ueber-uns" "https://acme.de/impressum" "https://acme.de/kontakt"]
                eu/vocabulary)]
    (is (= "https://acme.de/ueber-uns" (last ranked)))
    (is (contains? #{"https://acme.de/impressum" "https://acme.de/kontakt"} (first ranked)))))

(deftest contact-page-detection-in-eu-languages
  (is (true? (cp/contact-page? "<p>Kontakt</p><input type=\"text\">" eu/contact-text-re)))
  (is (true? (cp/contact-page? "<p>Contatti</p>Telefono: 06 123" eu/contact-text-re)))
  (is (false? (cp/contact-page? "<p>Nur ein Produkt</p>" eu/contact-text-re))))

(deftest coverage-separates-what-was-not-measured
  (let [recs [(eu/->record {:name "A" :street "X" :city "Wien" :vat "AT1"}
                           {:status :ok :observed-at "t" :contact-url "https://a/kontakt"})
              (eu/->record {:name "B" :street "Y" :city "Roma" :contact-form "https://ec.europa.eu/x"}
                           {:status :fetch-failed :observed-at "t"})
              (eu/->record {:name "C" :street "Z" :city "Paris" :contact-form "https://ec.europa.eu/y"}
                           {:status :no-contact-point :observed-at "t"})]
        c (eu/coverage recs)]
    (is (= 3 (:coverage/scanned c)))
    (is (= {:ok 1 :fetch-failed 1 :no-contact-point 1} (:coverage/by-status c))
        "見に行けなかった行と、見に行って無かった行を同じに数えない")
    (is (= 3 (:coverage/contactable c))
        "3 件とも宛名は作れる。web は 1 件だけ")
    (is (= 1 (:coverage/contactable-web c)))
    (is (= 2 (:coverage/contactable-post c)))
    (is (= 2 (:coverage/with-eu-portal-form c))
        "使わない面の大きさも測って残す")
    (is (= 1 (:coverage/with-vat c)))))

(deftest record-carries-the-cordis-identity
  (let [org (cordis/organisation
             [(cordis/->participation {:organisation-id "912743326" :vat "ESB98896137"
                                       :name "LOMARTOV SL" :street "CALLE ALFARERIA 3"
                                       :post-code "46100" :city "BURJASSOT" :country "ES"
                                       :topic "HORIZON-CL4-2025-DATA-01-01"
                                       :signature-date "2025-02-10" :sme "true"})])
        rec (eu/->record org {:status :no-website :observed-at "t"})]
    (is (= "912743326" (:company/cordis-organisation-id rec)))
    (is (= "ESB98896137" (:company/vat-number rec)))
    (is (= "ES" (:company/jurisdiction rec)))
    (is (= "CALLE ALFARERIA 3, 46100, BURJASSOT, ES" (:company/location rec)))
    (is (true? (:company/sme rec)))))
