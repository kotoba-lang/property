(ns kotoba.property.domain-facts-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.property.domain-facts :as df]))

(def jprs-cojp
  "Domain Information: [ドメイン情報]
a. [ドメイン名]                 ADACHIKIKAISS.CO.JP
e. [そしきめい]                 かぶしきかいしゃ あだちきかいせいさくしょ
f. [組織名]                     株式会社 足立機械製作所
g. [Organization]               ADACHI MACHINERY CO.,LTD.
k. [組織種別]                   株式会社
m. [登録担当者]                 TE347JP
n. [技術連絡担当者]             KK1960JP
p. [ネームサーバ]               kotetsu.alpha-lt.net
p. [ネームサーバ]               tsukuba.aics.ne.jp
s. [署名鍵]
[状態]                          Connected (2027/03/31)
[登録年月日]                    2000/03/29
[接続年月日]                    2000/03/30
[最終更新]                      2026/04/01 01:00:49 (JST)

Contact Information: [公開連絡窓口]
[名前]                          山田 太郎
[電子メール]                    taro@example.co.jp
[電話番号]                      03-0000-0000")

(deftest jprs-whois-gives-the-registered-corporate-name
  (let [m (df/parse-jprs-whois jprs-cojp)]
    (is (= "株式会社 足立機械製作所" (:registry/registrant-name m)))
    (is (= "ADACHI MACHINERY CO.,LTD." (:registry/registrant-name-en m)))
    (is (= "株式会社" (:registry/organization-type m)))
    (is (= "2000-03-29" (:registry/created-on m)))
    (is (= "2026-04-01" (:registry/updated-at m)) "時刻とタイムゾーンは落とす")
    (is (= ["kotetsu.alpha-lt.net" "tsukuba.aics.ne.jp"] (:registry/nameservers m)))
    (is (false? (:registry/dnssec-signed? m)) "署名鍵欄が空 = 未署名")
    (is (= "jprs-whois" (:registry/source m)))))

(deftest jprs-whois-never-carries-a-person
  ;; allowlist なので、担当者ハンドルも公開連絡窓口の個人も入らない。
  ;; 「落とす」ではなく「拾わない」—— レジストリが項目を増やした日に守られる。
  (let [m (df/parse-jprs-whois jprs-cojp)
        blob (pr-str m)]
    (doseq [leak ["TE347JP" "KK1960JP" "山田" "太郎" "taro@example.co.jp" "03-0000-0000"]]
      (is (not (clojure.string/includes? blob leak)) (str "leaked: " leak)))))

(deftest rdap-takes-the-registrar-and-not-the-registrant
  (let [m (df/parse-rdap
           {"ldhName" "ABEAM.COM"
            "status" ["client transfer prohibited"]
            "events" [{"eventAction" "registration" "eventDate" "2002-07-07T00:00:00Z"}
                      {"eventAction" "expiration" "eventDate" "2027-07-07T00:00:00Z"}
                      {"eventAction" "last changed" "eventDate" "2022-03-25T00:00:00Z"}]
            "nameservers" [{"ldhName" "NS00.VIPS.NE.JP"}]
            "secureDNS" {"delegationSigned" false}
            "entities" [{"roles" ["registrar"]
                         "vcardArray" ["vcard" [["version" {} "text" "4.0"]
                                                ["fn" {} "text" "MarkMonitor Inc."]]]}
                        {"roles" ["registrant"]
                         "vcardArray" ["vcard" [["fn" {} "text" "Jane Doe"]]]}]})]
    (is (= "abeam.com" (:registry/domain m)))
    (is (= "2002-07-07" (:registry/created-on m)))
    (is (= ["ns00.vips.ne.jp"] (:registry/nameservers m)))
    (is (= "MarkMonitor Inc." (:registry/registrar m)))
    (is (false? (:registry/dnssec-signed? m)))
    (is (not (clojure.string/includes? (pr-str m) "Jane Doe")) "registrant は見ない")))

(deftest registrable-domain-does-not-over-shorten
  (is (= "example.co.jp" (df/registrable-domain "ir.example.co.jp")))
  (is (= "example.co.jp" (df/registrable-domain "example.co.jp")))
  (is (= "example.jp" (df/registrable-domain "www.example.jp")))
  (is (= "example.com" (df/registrable-domain "news.example.com")))
  ;; 知らない 2 段接尾辞は縮めない（別の会社に聞きに行くより聞けない方がよい）。
  (is (= "example.com" (df/registrable-domain "example.com"))))

(deftest url-to-host
  (is (= "example.co.jp" (df/url->host "https://www.example.co.jp/company/")))
  (is (= "example.co.jp" (df/url->host "http://example.co.jp:8080")))
  (is (nil? (df/url->host "https://192.168.0.1/")))
  (is (nil? (df/url->host ""))))

(deftest dns-facts-separate-unmeasured-from-absent
  (testing "引けなかったときに『SPF が無い』と言わない"
    (let [m (df/dns-facts {:queried? false :observed-at "2026-08-19T00:00:00Z"})]
      (is (false? (:dns/queried? m)))
      (is (not (contains? m :dns/spf?)))))
  (let [m (df/dns-facts {:queried? true
                         :ns ["NS1.EXAMPLE.JP"]
                         :mx ["aspmx.l.google.com"]
                         :txt ["v=spf1 include:_spf.google.com ~all"]
                         :observed-at "2026-08-19T00:00:00Z"})]
    (is (true? (:dns/spf? m)))
    (is (= "google" (:dns/mail-provider m)))
    (is (= ["ns1.example.jp"] (:dns/nameservers m)))))

(deftest registrant-agreement-ignores-spacing
  (is (true? (df/registrant-agrees? "株式会社 足立機械製作所" "株式会社足立機械製作所")))
  (is (false? (df/registrant-agrees? "株式会社ほか" "株式会社足立機械製作所")))
  (is (nil? (df/registrant-agrees? nil "株式会社足立機械製作所"))))

(deftest generic-jp-registrant-may-be-a-person
  ;; 汎用 .jp は個人でも取れる。実測 2026-08-19、`shikigaku.jp` の登録者は
  ;; 会社ではなく個人名だった（会社自体は登記法人）。
  (let [m (df/parse-jprs-whois
           "Domain Information:\n[Domain Name]                   SHIKIGAKU.JP\n[登録者名]                      小川 大介\n[Name Server]                   ns1.example.jp\n[Created on]                    2015/04/01")]
    (is (nil? (:registry/registrant-name m)))
    (is (true? (:registry/registrant-withheld? m)) "黙って消さない")
    (is (= ["ns1.example.jp"] (:registry/nameservers m)) "他の事実は残る"))
  ;; 属性型（co.jp）は組織にしか割り当てられないので、そのまま採る。
  (let [m (df/parse-jprs-whois
           "Domain Information:\n[ドメイン名]                    EXAMPLE.CO.JP\n[組織名]                        株式会社なんとか\n[登録年月日]                    2001/01/01")]
    (is (= "株式会社なんとか" (:registry/registrant-name m)))
    (is (not (contains? m :registry/registrant-withheld?))))
  ;; 汎用 .jp でも法人格が名前に出ていれば組織として採る。
  (let [m (df/parse-jprs-whois
           "Domain Information:\n[Domain Name]                   ADK.JP\n[Registrant]                    ADK Holdings Inc.\n[Created on]                    2020/02/13")]
    (is (= "ADK Holdings Inc." (:registry/registrant-name-en m)))))

(deftest name-agreement-sees-through-fullwidth
  ;; 法人番号側は全角（ＮＴＴ）、レジストリは半角（NTT）を使う。
  (is (true? (df/registrant-agrees? "NTTインフラネット株式会社" "ＮＴＴインフラネット株式会社")))
  (is (true? (df/registrant-agrees? "株式会社 Works Human Intelligence" "株式会社Ｗｏｒｋｓ　Ｈｕｍａｎ　Ｉｎｔｅｌｌｉｇｅｎｃｅ")))
  ;; 本物の不一致は不一致のまま（親会社がグループのドメインを持っている）。
  (is (false? (df/registrant-agrees? "オリックス株式会社" "オリックス・レンテック株式会社"))))
