(ns export-ownership-datoms
  "var/ collector store -> data/property-ownership.datoms.edn (committed).

   Usage:
     nbb -cp src scripts/export_ownership_datoms.cljs
     nbb -cp src scripts/export_ownership_datoms.cljs \\
       --store var/kotoba-property/nyc-owned-properties.edn \\
       --out data/property-ownership.datoms.edn"
  (:require [cljs.reader :as reader]
            [kotoba.property.plane-export :as pe]
            ["fs" :as fs]
            ["path" :as path]))

(def default-store "var/kotoba-property/nyc-owned-properties.edn")
(def default-out "data/property-ownership.datoms.edn")

(defn- arg-value [args option default]
  (or (second (drop-while #(not= option %) args)) default))

(defn -main []
  (let [args (vec *command-line-args*)
        store-path (arg-value args "--store" default-store)
        out (arg-value args "--out" default-out)]
    (when-not (.existsSync fs store-path)
      (println (str "no collector store at " store-path
                    " — run scripts/collect_nyc.cljs first"))
      (.exit js/process 2))
    (let [store (reader/read-string (.readFileSync fs store-path "utf8"))
          records (pe/store->records store)
          observed-at (->> (vals (:ownership-records store))
                           (keep :ownership/observed-at)
                           sort
                           last)
          manifest (pe/ownership-manifest {:observed-at observed-at
                                           :sources (pe/store-sources store)
                                           :record-count (count records)})]
      (.mkdirSync fs (.dirname path out) #js {:recursive true})
      (.writeFileSync fs out
                      (str (pr-str manifest) "\n"
                           (apply str (map #(str (pr-str %) "\n") records)))
                      "utf8")
      (println (pr-str {:out out
                        :records (count records)
                        :sources (sort (pe/store-sources store))
                        :observed-at observed-at
                        :bytes (.-size (.statSync fs out))})))))

(-main)
